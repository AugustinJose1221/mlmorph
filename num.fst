#include "symbols.fst"

% Numbers represented in text format has limited vocabulary because of obvious reason
% The lexicon for that is given below.
$half$ = അര <half>
$n1$ = ഒന്ന് | ഒന്നു്
$n11-19$ = { പത്ത് <tens> ഒന്ന്} : {പതിനൊന്ന്} |\
	{ പത്ത് <tens> രണ്ട്} : {പന്ത്രണ്ട്} |\
	{ പത്ത് <tens> മൂന്ന്} : {പതിമൂന്ന്} |\
	{ പത്ത് <tens> നാല്} : {പതിനാല്} |\
	{ പത്ത് <tens> അഞ്ച്} : {പതിനഞ്ച്} |\
	{ പത്ത് <tens> ആറ്} : {പതിനാറ്} |\
	{ പത്ത് <tens> ഏഴ്} : {പതിനേഴ്} |\
	{ പത്ത് <tens> എട്ട്} : {പതിനെട്ട്} |\
	{ പത്ത് <tens> ഒമ്പത്} : {പത്തൊമ്പത്}

$n0$ = പൂജ്യം <zero>
$n2-9$ = (രണ്ട്|മൂന്ന്|നാല്|അഞ്ച്|ആറ്|ഏഴ്|എട്ട്|ഒമ്പത്)

% ones
$N1$ = ( $n1$ | $n2-9$ ) <ones>

$n10-90$ = (പത്ത് | ഇരുപത്|മുപ്പത്|നാല്പത്|അമ്പത്|അറുപത്|എഴുപത്|എൺപത് | തൊണ്ണൂറ്)
$n20-90-compound$ = {ഇരുപത്}: {ഇരുപത്തി} |\
	{മുപ്പത്}: {മുപ്പത്തി} |\
	{നാല്പത്}: {നാല്പത്തി} |\
	{നാൽപത്}: {നാൽപത്തി} |\
	{അമ്പത്}: {അമ്പത്തി} |\
	{അൻപത്}: {അൻപത്തി} |\
	{അറുപത്}: {അറുപത്തി} |\
	{എഴുപത്}: {എഴുപത്തി} |\
	{എൺപത്}: {എൺപത്തി} |\
	{എമ്പത്}: {എമ്പത്തി} |\
	{തൊണ്ണൂറ്} : {തൊണ്ണൂറ്റി}

% tens
$N10$ = ( $n10-90$ <tens> ) | \
	( ( $n11-19$ <ones> ) | ( $n20-90-compound$ <tens> ) $N1$ )

% hundreds
$n100-900$ = (നൂറ് | ഇരുനൂറ് | മുന്നൂറ് | നാനൂറ് | അഞ്ഞൂറ് | അറുനൂറ് | എഴുന്നൂറ് | എണ്ണൂറ് | തൊള്ളായിരം)
$n100-900-compound$ = {നൂറ്} : {നൂറ്റി} |\
	{ഇരുനൂറ്} : {ഇരുനൂറ്റി} |\
	{മുന്നൂറ്} : {മുന്നൂറ്റി} |\
	{നാനൂറ്} : {നാനൂറ്റി} |\
	{അഞ്ഞൂറ്} : {അഞ്ഞൂറ്റി} |\
	{അറുനൂറ്} : {അറുനൂറ്റി} |\
	{എഴുന്നൂറ്} : {എഴുനൂറ്റി} |\
	{എണ്ണൂറ്} : {എണ്ണൂറ്റി} |\
	{തൊള്ളായിരം} : {തൊള്ളായിരത്തി}

$N100$ = ( $n100-900$ <hundreds> ) |\
	( $n100-900-compound$ <hundreds> $N10$ )


% thousands
$n1k-9k$ = ആയിരം | രണ്ടായിരം | മുവ്വായിരം | നാലായിരം | അയ്യായിരം | ആറായിരം | ഏഴായിരം | എണ്ണായിരം | ഒമ്പതിനായിരം
$n1k-9k-prefix$ = {ആയിരം} : {ആയിരത്തി} |\
	{രണ്ടായിരം} : {രണ്ടായിരത്തി} |\
 	{മുവ്വായിരം} : {മുവ്വായിരത്തി} |\
	{നാലായിരം} : {നാലായിരത്തി} |\
	{അയ്യായിരം} : {അയ്യായിരത്തി} |\
	{ആറായിരം} : {ആറായിരത്തി} |\
	{ഏഴായിരം} : {ഏഴായിരത്തി} |\
	{എണ്ണായിരം} : {എണ്ണായിരത്തി} |\
	{ഒമ്പതിനായിരം} : {ഒമ്പതിനായിരത്തി}

$N1K$ = ( $n1k-9k$ <thousands> ) |\
	( $N10$? $n1k-9k-prefix$ <thousands> $N100$ )

$n1l$ = ലക്ഷം <lakhs>
$N1L$ = ( $N1$|$N10$|$N100$ )? $n1l$ ($N1K$|$N100$|$N10$|$N1$)?

$n1c$ = കോടി <crores>
$N1C$ = ( $N1$|$N10$|$N100$ )? $n1c$ ($N1L$|$N1K$|$N100$|$N10$|$N1$)?

$numbers$ = ( $n0$ | $N1$ | $N10$ | $N100$ | $N1K$ ) $half$?

<LB> $numbers$ <num><RB>
